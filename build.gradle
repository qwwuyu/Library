buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url "https://jitpack.io"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    def stream = project.rootProject.file('local.properties').newDataInputStream()
    properties.load(stream)
    stream.close()
}
ext.getPropertyByName = { key, defValue ->
    def value = properties.getProperty(key)
    if (value == null) value = System.getenv(key)
    return value != null ? value : defValue
}

ext {
    int major = 0, minor = 0, revision = 1
    configs = [
            compileSdkVersion: 28,
            minSdkVersion    : 15,
            targetSdkVersion : 28,
            versionCode      : major * 10000 + minor * 100 + revision,//max=2147483647
            versionName      : major + "." + minor + "." + revision,
            currentDate      : new Date().format('yyMMdd-HH'),
            test             : getPropertyByName("APP_TEST_ENABLE", "false"),
    ]

    def storeFile = getPropertyByName("STORE_FILE", null)
    signInfo = [
            isSign       : storeFile != null,
            storeFile    : storeFile,
            storePassword: getPropertyByName("STORE_PASSWORD", null),
            keyAlias     : getPropertyByName("KEY_ALIAS", null),
            keyPassword  : getPropertyByName("KEY_PASSWORD", null),
    ]

    versions = [
            junit           : "junit:junit:4.12",
            constraintlayout: "androidx.constraintlayout:constraintlayout:1.1.3",
            appcompat       : "androidx.appcompat:appcompat:1.0.0",
            annotation      : "androidx.annotation:annotation:1.0.0",
            recyclerview    : "androidx.recyclerview:recyclerview:1.0.0",

            rxjava2         : "2.2.7",
            rxandroid2      : "2.1.1",
            rxlifecycle2    : "2.2.1",
            okhttp3         : "3.10.0",
            retrofit        : "2.4.0",
            glide           : "4.10.0",
    ]
}
