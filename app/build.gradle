apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.getkeepsafe.dexcount'
dexcount {
    includeTotalMethodCount = true
}

android {
    compileSdkVersion configs.compileSdkVersion

    defaultConfig {
        applicationId "com.qwwuyu.example"
        minSdkVersion configs.minSdkVersion
        targetSdkVersion configs.targetSdkVersion
        versionCode configs.versionCode
        versionName configs.versionName
        // dex突破65535的限制
        multiDexEnabled true
        // 设置支持的SO库架构
        //ndk { abiFilters 'armeabi', 'armeabi-v7a', 'armeabi-v8a', 'x86', 'x86-64' }
        // 替换manifest属性
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "baidu"]
        // 设置BuildConfig
        buildConfigField "String", "BASE_URL", "\"https://www.qwwuyu.com\""
    }

    if (signInfo.isSign) {
        signingConfigs {
            sign {
                storeFile file(signInfo.storeFile)
                storePassword signInfo.storePassword
                keyAlias signInfo.keyAlias
                keyPassword signInfo.keyPassword
            }
        }
    }

    buildTypes {
        debug {
            if (signInfo.isSign) signingConfig signingConfigs.sign
            versionNameSuffix ".debug"
            debuggable true
            zipAlignEnabled false
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
            shrinkResources false
            ndk { abiFilters "armeabi", "x86" }
            buildConfigField "boolean", "TEST", "$configs.test"
        }
        release {
            if (signInfo.isSign) signingConfig signingConfigs.sign
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            shrinkResources true
            ndk { abiFilters "armeabi", "x86" }
            buildConfigField "boolean", "TEST", "false"
        }
        ts {
            initWith debug
            matchingFallbacks = ['debug']
            versionNameSuffix ".ts"
            buildConfigField "String", "BASE_URL", "\"https://old.qwwuyu.com\""
        }
    }

    flavorDimensions "log", "channel"
    productFlavors {
        log {
            dimension "log"
            buildConfigField "boolean", "LOG", "true"
        }
        nolog {
            dimension "log"
            buildConfigField "boolean", "LOG", "false"
        }
        baidu {
            dimension "channel"
        }
        qq {
            dimension "channel"
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("nolog") && variant.buildType.name != "release") {
            setIgnore(true)
        }
    }
    productFlavors.all { flavor ->
        if ("channel" == flavor.dimension) {
            flavor.addManifestPlaceholders([UMENG_CHANNEL_VALUE: name])
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def name = "v${variant.mergedFlavor.versionName}%s%s%s.apk"
            def channel = "", log = "", buildType = "_" + variant.buildType.name
            variant.productFlavors.each { flavor ->
                if (flavor.dimension == "channel") channel = "_" + flavor.name
                if (flavor.dimension == "log" && flavor.name == "log") log = "_" + flavor.name
            }
            outputFileName = String.format(name, channel, log, buildType)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    //test
    testImplementation "${versions.junit}"
    //support
    implementation "${versions.appcompat}"
    implementation "${versions.annotation}"
    implementation "${versions.recyclerview}"
    implementation "${versions.constraintlayout}"
    //project
    implementation project(':qw-lib')
    implementation project(':qw-widget')
    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-3'
    //glide
    annotationProcessor "com.github.bumptech.glide:compiler:$versions.glide"
}
