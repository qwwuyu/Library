apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.qwwuyu.lib' // Maven Group ID for the artifact (package name is ok)
version = '0.0.1' // This is the library version used when deploying the artifact
//项目目录下执行gradlew bintrayUpload,local.properties添加以下两行或配置环境变量.
//bintray.user=***
//bintray.apikey=***

def siteUrl = 'https://github.com/qwwuyu/Library'    // project homepage
def gitUrl = 'https://github.com/qwwuyu/Library.git' // project git

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'Less Code For Android'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'qiwei'                         //your user id (you can write your nickname)
                        name 'qiweiwuyu'                       //your name
                        email 'qiweiwuyu@gmail.com'         //your email
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    options.encoding = "utf-8"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += project.files(configurations.compile.files, android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
if (project.rootProject.file('local.properties') != null) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}
bintray {
    user = properties.getProperty("bintray.user") != null ? properties.getProperty("bintray.user") : System.getenv("BINTRAY_USER")
    key = properties.getProperty("bintray.apikey") != null ? properties.getProperty("bintray.apikey") : System.getenv("BINTRAY_KEY")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "qw-lib"    // project name in jcenter
        userOrg = 'qiwei'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
javadoc { //java doc采用utf-8编码
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
    }
}